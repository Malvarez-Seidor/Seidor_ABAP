@Metadata.layer: #CORE

@UI :{ headerInfo : { typeName          : 'Withholding',
                      typeNamePlural    : 'Withholdings',
                      title             : { type :#STANDARD, label : 'Sociedad', value : 'CompanyCodeName' },
                      imageUrl: 'Xml',
                      description       : { type :#STANDARD,  value : 'CompanyCode' } },
 presentationVariant: [ { sortOrder: [ { by: 'DocumentStatus',  direction:  #DESC },
                                       { by: 'AccountingDocument', direction:  #ASC  } ],
                     visualizations: [ { type: #AS_LINEITEM } ] } ] }

annotate view ZCDS_P_DOC_RET with
{
  @UI.facet: [ { label: 'Header Data',   purpose: #STANDARD, type: #IDENTIFICATION_REFERENCE, importance: #HIGH, position: 10 },
             { label: 'Document Info',   purpose: #STANDARD, type: #FIELDGROUP_REFERENCE,     importance: #HIGH, position: 20, targetQualifier: 'Detail'   },
             { label: 'Attached',        purpose: #STANDARD, type: #FIELDGROUP_REFERENCE,     importance: #HIGH, position: 30, targetQualifier: 'Attached' },
             { label: 'Log',             purpose: #STANDARD, type: #FIELDGROUP_REFERENCE,     importance: #HIGH, position: 40, targetQualifier: 'Log' } ]

  @UI: { lineItem: [ { position : 10, importance : #HIGH } ],
  textArrangement: #TEXT_ONLY }
  @Search.defaultSearchElement: true
  @Search.fuzzinessThreshold: 0.8
  CompanyCode;

  @UI: { lineItem: [ { position : 20, importance : #HIGH } ],
  selectionField: [ { position : 10 } ] }

  @UI: { dataPoint: { visualization: #NUMBER  } }
  @UI.dataPoint.title: 'Accounting Document'
  AccountingDocument;
  
  @UI: { lineItem: [ { position : 30, importance : #MEDIUM } ],
   textArrangement: #TEXT_ONLY }
  @UI.dataPoint.title: 'Accounting Document Type'
  @Search.defaultSearchElement: true
  @ Search.fuzzinessThreshold: 0.8
  AccountingDocumentType;
  
  @UI: { lineItem: [ { position : 100,  label : 'Document Status', importance : #HIGH, criticality: 'Criticality' },
                     { type: #FOR_ACTION, dataAction: 'SendDocument', label: 'Send Document' },
                     { type: #FOR_ACTION, dataAction: 'UpdateStatus', label: 'Update Status' } ],
   selectionField: [ { position : 60 } ],
   identification: [ { type: #FOR_ACTION, dataAction: 'SendDocument', label: 'Send Document' },
                     { type: #FOR_ACTION, dataAction: 'UpdateStatus', label: 'Update Status' } ] }
  
  @UI.textArrangement: #TEXT_ONLY
  @UI: { dataPoint: { title: 'Document Status',  criticality : 'Criticality', criticalityRepresentation: #WITH_ICON } }
  @Search.defaultSearchElement: true
  @Search.fuzzinessThreshold: 0.8
  DocumentStatus;
  
  @UI: { lineItem: [ { position : 140, label : 'Document Type', importance : #MEDIUM } ],
   identification: [ { position : 10,  label : 'Document Type' } ],
   textArrangement: #TEXT_ONLY }
  @Search.defaultSearchElement: true
  @Search.fuzzinessThreshold: 0.8
  DocumentType;
  
  @UI: { lineItem: [ { position : 50,  importance : #HIGH } ],
   selectionField: [ { position : 20 } ],
   textArrangement: #TEXT_ONLY  }
  @UI: { identification: [ { position : 20 } ] }
  @Search.defaultSearchElement: true
  @Search.fuzzinessThreshold: 0.8
  Supplier;
  
  @UI: { lineItem: [ { position : 60, label : 'Establishment', importance : #HIGH } ],
   identification: [ { position : 30, label : 'Establishment' } ],
   selectionField: [ { position: 30 } ] }
  Establishment;
  
  @UI: { lineItem: [ { position : 130, label : 'Accesskey', importance : #MEDIUM } ],
   identification: [ { position : 40, label : 'Accesskey' } ] }
  Accesskey;
  
  @UI: { lineItem: [ { position : 90, label : 'Issue Date', importance : #MEDIUM } ],
   identification: [ { position : 50, label : 'Issue Date' } ] }
  IssueDate;
  
  @UI: { identification: [ { position : 60, label : 'Type Id' } ] }
  TypeId;
  
  @UI: { lineItem: [ { position : 70, label : 'Emission Point', importance : #HIGH } ],
   identification: [ { position : 70, label : 'Emission Point' } ],
   selectionField: [ { position: 40 } ] }
  EmissionPoint;
  
  @UI: { identification: [ { position : 80 } ] }
  ReverseDocument;
  
  @UI: { lineItem: [ { position : 110, label : 'Authorization Date', importance :  #MEDIUM } ],
         identification: [ { position : 90, label : 'Authorization Date' } ] }
  AuthorizationDate;
  
  @UI: { identification: [ { position : 100, label : 'Id Number' } ] }
  IdNumber;
  
  @UI: { lineItem: [ { position : 80, label : 'Sequential', importance : #HIGH } ],
   identification: [ { position : 110,  label : 'Sequential' } ],
   selectionField: [ { position: 50 } ] }
  Sequential;
  
  @UI: { identification: [ { position : 120,  label : 'Document Supplier' } ] }
  DocumentSupplier;
  
  @UI: { lineItem: [ { position : 120, label : 'Created By User', importance : #MEDIUM } ],
   textArrangement: #TEXT_ONLY }
  @UI.fieldGroup: [ { qualifier: 'Detail',position: 10 } ]
  AccountingDocCreatedByUser;
  
  @UI: { lineItem: [ { position : 150, label : 'Posting Date', importance : #HIGH } ] }
  @UI.fieldGroup: [ { qualifier: 'Detail',position: 40 } ]
  PostingDate;
  
  @UI.fieldGroup: [ { qualifier: 'Detail',position: 50 } ]
  NetAmountIsPosted;
  
  @UI.fieldGroup: [ { qualifier: 'Detail', label: 'Header Text', position: 60 } ]
  AccountingDocumentHeaderText;
  
  @UI: { lineItem: [ { position : 130, label : 'Created Date', importance : #MEDIUM } ] }
  @UI.fieldGroup:  [ { qualifier: 'Detail', position: 70 } ]
  AccountingDocumentCreationDate;
  
  @UI: { lineItem: [ { position : 40, importance : #MEDIUM } ] }
  @UI.fieldGroup: [ { qualifier: 'Detail',position: 90 } ]
  FiscalYear;
  
  @UI.fieldGroup: [ { qualifier: 'Detail',position: 100 } ]
  TaxBaseAmountIsNetAmount;
  
  @UI.fieldGroup: [ { qualifier: 'Detail',position: 110 } ]
  DocumentReferenceID;
  
  @UI.fieldGroup: [ { qualifier: 'Detail',position: 120 } ]
  DocumentItemText;
  
  @UI.fieldGroup: [ { qualifier:'Attached',  label :'Xml', position: 10 } ]
  Xml;
  
  @UI.fieldGroup: [ { qualifier: 'Log', label : 'Message Document', position: 10 } ]
  MessageDocument;
  
  @Search.defaultSearchElement: true
  @Search.fuzzinessThreshold: 0.8
  BusinessName;
  
  @Search.defaultSearchElement: true
  @Search.fuzzinessThreshold: 0.8
  CompanyCodeName;
  
  @Search.defaultSearchElement: true
  @Search.fuzzinessThreshold: 0.8
  UserFullName;
  
  @Search.defaultSearchElement: true
  @Search.fuzzinessThreshold: 0.8
  AccountingDocumentTypeName;

}